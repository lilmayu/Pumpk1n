plugins {
    id 'java'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id 'jacoco'
    id 'jacoco-report-aggregation'
}

group 'dev.mayuna'
version '2.2.1'

repositories {
    mavenCentral()
}

dependencies {
    // Personal libraries
    implementation 'dev.mayuna:mayus-json-utilities:2.0'
    testImplementation 'dev.mayuna:mayus-json-utilities:2.0'

    // GSON
    compileOnly 'com.google.code.gson:gson:2.11.0'
    testImplementation 'com.google.code.gson:gson:2.11.0'

    // SQLite
    compileOnly 'org.xerial:sqlite-jdbc:3.46.0.0'
    testImplementation 'org.xerial:sqlite-jdbc:3.46.0.0'

    // MySQL
    compileOnly 'com.mysql:mysql-connector-j:8.4.0'
    testImplementation 'com.mysql:mysql-connector-j:8.4.0'

    // HikariCP
    compileOnly 'com.zaxxer:HikariCP:3.4.5'
    testImplementation 'com.zaxxer:HikariCP:3.4.5'

    // slf4j api
    compileOnly 'org.slf4j:slf4j-api:2.0.13'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.+'
    annotationProcessor 'org.projectlombok:lombok:1.18.+'
    testCompileOnly 'org.projectlombok:lombok:1.18.+'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.+'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

// Java 8
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

tasks.jacocoTestReport {
    reports {
        csv.required = true
    }
}

// == Quick tasks == //

task publishCloseAndRelease() {
    dependsOn 'publishToSonatype'
    dependsOn 'closeAndReleaseSonatypeStagingRepository'
}

shadowJar {

}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

// == Maven publishing == //

publishing {

    publications {
        shadow(MavenPublication) {
            groupId = 'dev.mayuna'
            artifactId = 'pumpk1n'
            version = getVersion()
            from components.java

            pom {
                name = 'Pumpk1n'
                description = 'Lightweight and customizable tool for quick data management using Gson serialization'
                url = 'https://github.com/lilmayu/Pumpk1n'

                scm {
                    connection = 'scm:https://github.com/lilmayu/Pumpk1n'
                    developerConnection = 'scm:git:https://github.com/lilmayu/Pumpk1n.git'
                    url = 'https://github.com/lilmayu/Pumpk1n'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'mayuna'
                        name = 'Marek Lof'
                        email = 'uwulilmayu@gmail.com'
                    }
                }
            }
        }
    }

    publishing {
        repositories {
            maven {
                credentials {
                    username = project.properties['ossrhUsername'] ?: System.getenv('ossrhUsername')
                    password = project.properties['ossrhPassword'] ?: System.getenv('ossrhPassword')
                }

                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.shadow
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = project.properties['ossrhUsername'] ?: System.getenv('ossrhUsername')
            password = project.properties['ossrhPassword'] ?: System.getenv('ossrhPassword')
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

shadowJar.dependsOn javadocJar
shadowJar.dependsOn sourcesJar
shadowJar.dependsOn jar

components.java.withVariantsFromConfiguration(configurations.shadowRuntimeElements) {
    skip()
}
